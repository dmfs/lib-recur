apply plugin: 'java-library'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def gitVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

configurations {
    pom
}

apply from: 'publish.gradle'
apply from: 'jacoco.gradle'

allprojects {
    group 'org.dmfs'
    version gitVersion()
    repositories {
        mavenCentral()
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    api 'org.dmfs:rfc5545-datetime:0.3'
    implementation 'org.dmfs:jems2:2.11.1'
    testImplementation project("lib-recur-hamcrest")
    testImplementation 'org.dmfs:jems2-testing:2.11.1'
}


test {
    useJUnitPlatform()
}
